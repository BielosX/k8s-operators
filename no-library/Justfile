timestamp := `date +%s`
localbin := justfile_directory() + "/bin"
kustomize := localbin + "/kustomize"
kustomize-version := "5.6.0"
kustomize-url := "https://raw.githubusercontent.com/kubernetes-sigs/kustomize/master/hack/install_kustomize.sh"

get-kustomize:
    #!/bin/bash -e
    mkdir -p "{{localbin}}"
    cd "{{localbin}}"
    if ! test -x kustomize || ! {{kustomize}} version | grep -q "{{kustomize-version}}"; then
        rm -rf "{{kustomize}}"
        curl -s "{{kustomize-url}}" | bash -s "{{kustomize-version}}"
    fi

img-build:
    rm -rf target
    podman build --tag 'no-library:{{timestamp}}' .
    mkdir -p target
    podman save -o target/no-library.tar 'no-library:{{timestamp}}'

kind-load: img-build
    kind load image-archive target/no-library.tar

k8s-apply: get-kustomize kind-load
    {{kustomize}} build config | IMAGE="localhost/no-library:{{timestamp}}" envsubst | kubectl apply -f -

k8s-delete: get-kustomize
    {{kustomize}} build config | kubectl delete -f -

example-apply: get-kustomize
    {{kustomize}} build examples | kubectl apply -f -

podman-prune-cache:
    podman image prune --build-cache

fmt:
    cargo fmt