timestamp := `date +%s`
localbin := justfile_directory() + "/bin"
kustomize := localbin + "/kustomize"
kustomize-version := "5.6.0"
kustomize-url := "https://raw.githubusercontent.com/kubernetes-sigs/kustomize/master/hack/install_kustomize.sh"
tag := env('TAG', timestamp)

run:
    go run main.go

build:
    mkdir -p bin
    go build -o bin

get-kustomize:
    #!/bin/bash -e
    mkdir -p "{{localbin}}"
    cd "{{localbin}}"
    if ! test -x kustomize || ! {{kustomize}} version | grep -q "{{kustomize-version}}"; then
        rm -rf "{{kustomize}}"
        curl -s "{{kustomize-url}}" | bash -s "{{kustomize-version}}"
    fi

img-build:
    rm -rf target
    podman build --tag 'minimal-controller:{{tag}}' .
    mkdir -p target
    podman save -o target/minimal-controller.tar 'minimal-controller:{{tag}}'

kind-load: img-build
    kind load image-archive target/minimal-controller.tar

k8s-apply: get-kustomize kind-load
    {{kustomize}} build config | IMG="localhost/minimal-controller:{{tag}}" envsubst | kubectl apply -f -
